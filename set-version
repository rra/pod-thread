#!/usr/bin/perl
#
# Set the version of all modules to the distribution version.
#
# This script should be invoked via the PM_FILTER setting in Makefile.PL.  It
# reads the distribution version from a file named VERSION and then sets the
# version of all modules to match.
#
# Copyright 2013 Russ Allbery <rra@stanford.edu>
#
# This program is free software; you may redistribute it and/or modify it
# under the same terms as Perl itself.

use 5.008;
use strict;
use warnings;

use File::Spec;
use Readonly;

# Regex matching the line setting $VERSION in a module.
Readonly my $VERSION_REGEX => qr{
    \A (                        # capture the leading text (1)
        \s* (?: our | my ) \s*  #   optional leading our or my
        \$VERSION \s* = \s*     #   $VERSION setting
    )                           # end part being preserved
    '? \S+ '? ;                 # version setting with optional quotes
    \s* \z                      # trailing whitespace
}xms;

# Load the distribution version.
our $VERSION;
if (!eval { require File::Spec->catfile(File::Spec->curdir(), 'VERSION') }) {
    die "Cannot load version information from VERSION: $!\n";
}

# Find the line that sets $VERSION and update it.
while (defined(my $line = <>)) {
    if ($line =~ s{ $VERSION_REGEX }{$1'$VERSION';\n}xms) {
        print {*STDOUT} $line or die "Cannot write to STDOUT: $!\n";
        last;
    }
    print {*STDOUT} $line or die "Cannot write to STDOUT: $!\n";
}

# Copy the remaining input to the output verbatim.
while (defined(my $line = <>)) {
    print {*STDOUT} $line or die "Cannot write to STDOUT: $!\n";
}
